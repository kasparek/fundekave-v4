<?xml version="1.0" encoding="utf-8"?>
<fu:Container xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:fu="net.fundekave.*"
	xmlns:bit101="com.bit101.components.*"
	width="{Fuup.WIDTH}" height="150">
	<fx:Script>
		<![CDATA[
			import gs.easing.Quad;
			import gs.TweenLite;
			import net.fundekave.lib.BitmapDataProcess;
			import net.fundekave.fuup.model.vo.FileVO;
			
			import flash.utils.setTimeout;
			import flash.display.Loader;
			
			public static const FILE_CREATED:String = 'fileCreated';
			public static const SETTINGS_INHERIT:String = 'settingsInherit';
			
			[Bindable]
			private var thumbMaxWidth:Number = 140;
			[Bindable]
			private var thumbMaxHeight:Number = 140;
			
			[Bindable]
			public var statusStr:String = '';
			
			[Bindable]
			private var _fileVO:FileVO;
			
			public function get fileVO():FileVO {
				return _fileVO;
			}
			public function set fileVO(value:FileVO):void {
				_fileVO = value;
			}
			
			public function get file():FileReference {
				return _fileVO.file;
			}
			public function set file(value:FileReference):void {
				_fileVO = new FileVO( value.name );
				_fileVO.file = value;
				_fileVO.renderer = this;
				
				value.load()
				value.addEventListener(Event.COMPLETE, imageLoaded);
			}
			
			private function imageLoaded(e:Event):void {
				var file:FileReference = e.target as FileReference;
				file.removeEventListener(Event.COMPLETE, imageLoaded);
				var byteData:ByteArray = file.data as ByteArray;
				var thumb:Loader = new Loader();
				thumb.loadBytes( byteData );
				thumbUI.addChild(thumb);
				thumb.contentLoaderInfo.addEventListener( Event.COMPLETE, drawLater);
			}
			
			/**
			 * CREATE SMALL THUMBNAIL
			*/
			[Bindable]
			private var wThumb:Number;
			[Bindable]
			private var hThumb:Number;
			private function drawLater(e:Event):void {
				var thumb:Loader = e.target.loader as Loader;
				thumb.contentLoaderInfo.removeEventListener( Event.COMPLETE, drawLater);
								
				_fileVO.widthOriginal = (thumb.content as Bitmap).width;
				_fileVO.heightOriginal = (thumb.content as Bitmap).height;
				
				/* The size of the output image */
				var scaled:Object = BitmapDataProcess.scaleCalc(_fileVO.widthOriginal,_fileVO.heightOriginal,thumbMaxWidth,thumbMaxHeight);
				wThumb = scaled.width;
				hThumb = scaled.height;
 				
 				var resized:BitmapData = new BitmapData(wThumb, hThumb , true, 0x000000);
 	
 				var sx:Number = wThumb / _fileVO.widthOriginal;
			  	var sy:Number = hThumb / _fileVO.heightOriginal;
 				var scale:Number = Math.min(sx, sy);
 				var m:Matrix = new Matrix();
				m.scale(scale, scale);
				resized.draw( thumb, m );
				
				thumb.parent.removeChild( thumb );

				var bmp:Bitmap = new Bitmap( resized );
				thumbUI.addChild( bmp );
				bmp.x = -wThumb / 2
				bmp.y = -hThumb / 2
				
				dispatchEvent( new Event( FILE_CREATED, true ));
			} 
			
			private var rotateTo:Number = 0;
			private function rotate(dgDiff:Number):void {
				rotateTo += dgDiff;
				
				TweenLite.to( _fileVO, 0.5, {rotation:rotateTo, ease:Quad.easeInOut, onComplete:onRotateTween} );
				
				if(rotateTo >= 360) rotateTo -= 360;
				if(rotateTo < 0) rotateTo += 360;  
				
			}
			private function onRotateTween():void {
				_fileVO.rotation = Number(rotateTo);
			}
			
			private var inheritWasTrue:Boolean = false;
			private function onInheritChange():void {
				if(resizeInheritCheckbox.selected == true && inheritWasTrue==true) {
					setTimeout( laterOnInheritChange, 1 );
				} else {
					inheritWasTrue = true;
				}
			}
			private function laterOnInheritChange():void {
				dispatchEvent(new Event(SETTINGS_INHERIT,true));
			}
			
			private function showResized():void {
				if(fileVO.encodedJPG) {
					var image:Loader = new Loader();
					image.loadBytes( fileVO.encodedJPG );
					this.addChild( image );
					image.addEventListener( MouseEvent.CLICK, onPreviewClick ); 
				}
			}
			
			private function onPreviewClick(e:Event):void {
				var image:Loader = e.currentTarget as Loader;
				image.removeEventListener( MouseEvent.CLICK, onPreviewClick );
				image.parent.removeChild( image );
			}
			
		]]>
	</fx:Script>
	
	<fx:Binding source="resizeInheritCheckbox.selected" destination="_fileVO.sizeInheritance" />
	<fx:Binding source="maxWidthInput.text as Number" destination="_fileVO.widthMax" />
	<fx:Binding source="maxHeightInput.text as Number" destination="_fileVO.heightMax" />
	
	<fu:Container width="{thumbMaxWidth}" height="{thumbMaxHeight}">
		<fu:Container id="thumbUI" rotation="{_fileVO.rotation}" x="{thumbMaxWidth/2}" y="{thumbMaxHeight/2}" width="{wThumb/2}" height="{hThumb/2}" />
	</fu:Container>
	
	<bit101:PushButton x="{this.width - 100}" label="Remove" click="{this.removeChild(this)}" />
	
	<bit101:VBox x="200">
		<bit101:HBox>
			<bit101:Label text="Filename:" />
			<bit101:InputText id="filenameInput" text="{_fileVO.filename}" restrict="[0-9][a-z][A-Z]_\-\." />
		</bit101:HBox>
		
		<bit101:HBox>
			<bit101:Label text="Max size:" />
			<bit101:InputText id="maxWidthInput" text="{_fileVO.widthMax}" visible="{!_fileVO.sizeInheritance}" width="40" restrict="[0-9]" />
			<bit101:Label text="x" />
			<bit101:InputText id="maxHeightInput" text="{_fileVO.heightMax}" visible="{!_fileVO.sizeInheritance}" width="40" restrict="[0-9]" />
			<bit101:CheckBox id="resizeInheritCheckbox" click="{onInheritChange()}" selected="{_fileVO.sizeInheritance}" label="Inherit" />
		</bit101:HBox>
		
		<bit101:HBox>
			<bit101:Label text="Rotation:" />
			<bit101:PushButton click="{rotate(90)}" label="CW" />
			<bit101:PushButton click="{rotate(-90)}" label="CCW" />
			<bit101:Label text="{_fileVO.rotation}" />
		</bit101:HBox>
		
		<bit101:PushButton label="Check resized" click="{showResized()}" />
		<!-- result size -->
		
		<!-- status bar -->
		<bit101:Label id="statusLbl" text="{statusStr}" />
	</bit101:VBox>
	
</fu:Container>