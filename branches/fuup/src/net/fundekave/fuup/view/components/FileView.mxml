<?xml version="1.0" encoding="utf-8"?>
<fu:Container xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:fu="net.fundekave.*"
	xmlns:bit101="com.bit101.components.*"
	width="175" height="{thumbMaxHeight + 10}"
	backgroundColor="{0xddddee}"
	>
	
	<fx:Script>
		<![CDATA[
			import gs.TweenLite;
			import gs.easing.Quad;
			
			import mx.events.CloseEvent;
			
			import net.fundekave.fuup.model.vo.FileVO;
			import net.fundekave.lib.ImageResize;
			
			import flash.filters.DropShadowFilter;
			import net.fundekave.Application;
			import com.bit101.components.Component;
			import com.bit101.components.Window;
			/**
			*import flash.filters.DropShadowFilte-r;
			*import net.fundekave.Applicatio-n;
			*import com.bit101.components.Componen-t;
			*import com.bit101.components.Windo-w;
			**/
			public static const FILE_CREATED:String = 'fileCreated';
			public static const FILE_UPDATED:String = 'fileUpdated';
			public static const SETTINGS_INHERIT:String = 'settingsInherit';
			public static const FILE_REMOVE:String = 'fileRemove';
			
			public static const STATE_PROCESSING:String = 'processing';
			
			public var lang:Object;
			
			[Embed(source="/assets/X.png")]
			private var ClosePNG:Class;
			[Embed(source="/assets/CCW.png")]
			private var CCWPNG:Class;
			[Embed(source="/assets/CW.png")]
			private var CWPNG:Class;
			[Embed(source="/assets/show.png")]
			private var ShowPNG:Class;
			
			[Bindable]
			private var thumbMaxWidth:Number = 140;
			[Bindable]
			private var thumbMaxHeight:Number = 100;
			
			[Bindable]
			public var statusStr:String = '';
			
			[Bindable]
			private var _fileVO:FileVO;
			
			public function get fileVO():FileVO {
				return _fileVO;
			}
			public function set fileVO(value:FileVO):void {
				_fileVO = value;
			}
			
			public function get file():FileReference {
				return _fileVO.file;
			}
			
			public function set file(value:FileReference):void {
				
				_fileVO = new FileVO( value.name );
				_fileVO.file = value;
				_fileVO.renderer = this;
											
				this.graphics.beginFill(0xbbbbbb);
				this.graphics.drawRoundRect(0,0,this.width,this.height,5,5);
				this.graphics.endFill();
				
				this.filters=[new DropShadowFilter(3,45,0,0.5,3,3)];
				
				var s1:Component = new Component();
				s1.useHandCursor = true;
				s1.buttonMode = true;
				s1.addEventListener(MouseEvent.CLICK, onButtClose,false,0,true);
				s1.width = 20;
				s1.height = 20;
				s1.addChild( new ClosePNG() );
				s1.filters=[new DropShadowFilter(2,45,0,0.5,2,2)];
				buttonBar.addChild( s1 );
				
				var s2:Component = new Component();
				s2.useHandCursor = true;
				s2.buttonMode = true;
				s2.addEventListener(MouseEvent.CLICK, onButtCCW,false,0,true);
				s2.width = 20;
				s2.height = 20;
				s2.addChild( new CCWPNG() );
				s2.filters=[new DropShadowFilter(2,45,0,0.5,2,2)];
				buttonBar.addChild( s2 );
				
				var s3:Component = new Component();
				s3.useHandCursor = true;
				s3.buttonMode = true;
				s3.addEventListener(MouseEvent.CLICK, onButtCW,false,0,true);
				s3.width = 20;
				s3.height = 20;
				s3.addChild( new CWPNG() );
				s3.filters=[new DropShadowFilter(2,45,0,0.5,2,2)];
				buttonBar.addChild( s3 );
				
				var s4:Component = new Component();
				s4.useHandCursor = true;
				s4.buttonMode = true;
				s4.addEventListener(MouseEvent.CLICK, onButtShow,false,0,true);
				s4.width = 20;
				s4.height = 20;
				s4.addChild( new ShowPNG() );
				s4.filters=[new DropShadowFilter(2,45,0,0.5,2,2)];
				buttonBar.addChild( s4 );
				
				updateThumb();
				
				dispatchEvent( new Event( FILE_CREATED, true ));
			}
			
			public function setLocalState(stateName:String):void {
				switch( stateName ) {
					case STATE_PROCESSING:
						fileVO.renderer.updateStatus(String(lang.processing),false);
						if(previewWin) {
							previewWin.close();
						}
						break;
				}
			}
						
			/**
			 * CREATE SMALL THUMBNAIL
			*/
			private function drawLater(e:Event):void {
				var imageResize:ImageResize = e.target as ImageResize;
				fileVO.widthOriginal = imageResize.widthOriginal; 
				fileVO.heightOriginal = imageResize.heightOriginal;

				var bmp:Bitmap = new Bitmap( imageResize.resultBmpData, PixelSnapping.NEVER, true );
				thumbUI.addChild( bmp );
				thumbUI.width = imageResize.resultBmpData.width;
				thumbUI.height = imageResize.resultBmpData.height;
				bmp.x = -imageResize.resultBmpData.width / 2
				bmp.y = -imageResize.resultBmpData.height / 2
				
				imageResize.dispose();
				
				updateStatus('');
				TweenLite.to(this.thumbUI,0.3,{alpha:1});
				dispatchEvent( new Event( FILE_UPDATED, true ));
			}
			
			public function updateThumb():void {
				if(this.thumbUI.numChildren > 0) {
					this.thumbUI.removeChildAt( 0 );
					rotateTo = 0;
				}
				this.thumbUI.alpha = 0;
				var imageResize:ImageResize = new ImageResize(thumbMaxWidth,thumbMaxHeight);
				if(fileVO.encodedJPG) {
					imageResize.loadBytes( fileVO.encodedJPG );
				} else {
					if(fileVO.file.data) {
						imageResize.loadBytes( fileVO.file.data );
					} else {
						imageResize.loadReference( fileVO.file );
					}
				}
				imageResize.addEventListener( ImageResize.RESIZED, drawLater ,false,0,true);
				this.thumbUI.addChild( imageResize );
			}
			
			public function updateStatus(status:String=null,showInfo:Boolean=true,type:int=0):void {
				switch(type) {
					case 1: //error
						//---set red background
						statusBar.backgroundColor = 0xffaa66;
						statusBar.backgroundAlpha = 0.9;
						statusBar.invalidate();
						break;
					default:
						//---set default background
						statusBar.backgroundColor = 0xffffff;
						statusBar.backgroundAlpha = 0.7;
						statusBar.invalidate();
				}
				if(status!==null) {
					this.statusStr = status;
				}
				if(showInfo)
				if(fileVO.encodedJPG) {
					statusStr += ' '+fileVO.widthNew+'x'+fileVO.heightNew+' '+String(Math.round(fileVO.encodedJPG.length/1024))+'kB';
				} else {
					statusStr += ' '+fileVO.widthOriginal+'x'+fileVO.heightOriginal+' '+String(Math.round(fileVO.file.size/1024))+'kB';	
				}
			}
			
			private var rotateTo:Number = 0;
			private function rotate(dgDiff:Number):void {
				rotateTo += dgDiff;
				
				TweenLite.to( _fileVO, 0.5, {rotation:rotateTo, ease:Quad.easeInOut, onComplete:onRotateTween} );
				
				if(rotateTo >= 360) rotateTo -= 360;
				if(rotateTo < 0) rotateTo += 360;  
				
			}
			private function onRotateTween():void {
				_fileVO.rotation = Number(rotateTo);
			}
			
			private var inheritWasTrue:Boolean = false;
			private function onInheritChange():void {
				/*
				if(resizeInheritCheckbox.selected == true && inheritWasTrue==true) {
					setTimeout( laterOnInheritChange, 1 );
				} else {
					inheritWasTrue = true;
				}
				*/
			}
			private function laterOnInheritChange():void {
				dispatchEvent(new Event(SETTINGS_INHERIT,true));
			}
			
			private var previewWin:Window;
			
			private function showResized():void {
				if(previewWin) {
					if(previewWin.minimized==true) {
						previewWin.minimized = false;
					}
					return;
				}
				
				var title:String = fileVO.filename
				+ ' ' 
				+ String((fileVO.widthNew)?(fileVO.widthNew):(fileVO.widthOriginal))
				+ 'x'
				+ String((fileVO.heightNew)?(fileVO.heightNew):(fileVO.heightOriginal))
				+ ' '
				+ String( Math.round(((fileVO.encodedJPG)?(fileVO.encodedJPG.length):(fileVO.file.size))/1024) ) + 'kB';
					
				
				previewWin = new Window(Application.application, 0, 0, title);
				previewWin.addEventListener( Event.RESIZE, onWinReady ,false,0,true);
				previewWin.draggable = true;
				previewWin.hasMinimizeButton = true;
				previewWin.hasCloseButton = true;
				
				previewWin.width = fileVO.widthOriginal;
				previewWin.height = fileVO.heightOriginal + 20;
				previewWin.addEventListener( CloseEvent.CLOSE, onClosePreview ,false,0,true);
				
				previewWin.x = -previewWin.width;
				previewWin.y = -previewWin.height;
				previewWin.closeTween = {alpha:0,ease:Quad.easeInOut};
				previewWin.closeTweenDuration = 0.2;
				
				if(previewWin.height > Application.application.height) {
					Application.application.height = previewWin.height + 10;
				}
			}
			
			private function onWinReady(e:Event):void {
				var win:Window = e.target as Window;
				win.removeEventListener( Event.RESIZE, onWinReady );
				TweenLite.to( win, 0.5, {x:0,y:0,ease:Quad.easeInOut,onComplete:onTweenPreviewWin,onCompleteParams:[win]} );
			}
			
			private function onTweenPreviewWin(win:Window):void {
				var image:Loader = new Loader();
				image.loadBytes( (fileVO.encodedJPG)?(fileVO.encodedJPG):(fileVO.file.data) );
				//image.x = 10;
				//image.y = 30;
				image.alpha = 0;
				image.contentLoaderInfo.addEventListener( Event.COMPLETE, onLoaderPreview ,false,0,true);
				win.content.addChild( image );
				
			}
			private function onLoaderPreview(e:Event):void {
				var loader:Loader = e.target.loader as Loader;
				loader.contentLoaderInfo.removeEventListener(Event.COMPLETE, onLoaderPreview );
				TweenLite.to( loader, 0.5, {alpha:1,ease:Quad.easeInOut} );
			}
						
			private function onClosePreview( e:CloseEvent ):void {
				previewWin = null;
				Application.application.restoreHeight();
			}
			
			public function removeView():void {
				TweenLite.to( this, 0.2, {alpha:0, onComplete:onTween, overwrite:0});
				trace("TWEENREMOVE"+fileVO.filename);
				if(previewWin) previewWin.close();
			}
			
			private function onTween():void {
				this.fileVO.destroy();
				this.fileVO = null;
				this.parent.removeChild( this );	
			}
			
			/**
			 * BUTTON HANDLERS
			 * */
			private function onButtClose(e:Event):void {
				dispatchEvent(new Event(FILE_REMOVE,true));
			}
			private function onButtCCW(e:Event):void {
				rotate(-90)
			}
			private function onButtCW(e:Event):void {
				rotate(90);
			}
			private function onButtShow(e:Event):void {
				showResized();
			}
			
		]]>
	</fx:Script>
	
	<fu:Container masked="true" backgroundColor="0x000000" border="0" x="5" y="5" width="{thumbMaxWidth}" height="{thumbMaxHeight}">
		<fu:Container id="thumbUI" rotation="{_fileVO.rotation}" x="{thumbMaxWidth/2}" y="{thumbMaxHeight/2}" />
	</fu:Container>
	
	<!-- status bar -->
	<fu:Container id="statusBar" x="5" width="{thumbMaxWidth}" y="{thumbMaxHeight-15}" height="15">
		<bit101:Label x="0" y="-2" id="statusLbl" text="{statusStr}" />
	</fu:Container>
	
	<bit101:VBox id="buttonBar" x="150" y="5" spacing="6" />
	
</fu:Container>